apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'


// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("../signing-keys/fifteen/.keys/keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


android {
    namespace 'ru.narod.nod.fifteen'
    compileSdk 34

    dexOptions {
        incremental = true
        preDexLibraries = false
        javaMaxHeapSize "5g" // 2g should be also OK
    }
    defaultConfig {
        applicationId "ru.narod.nod.fifteen"
        minSdk 21
        targetSdk 34
        versionCode commitCount()
        versionName "1.2.0"
    }
    signingConfigs {
        // Keys are kept on git@gitlab.com:neronovs/signing-keys.git
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        debug {
            buildConfigField "String", "PROGRAM_NAME", '"AnCalc DEBUG"'
        }
        release {
            signingConfig signingConfigs.release
            buildConfigField "String", "PROGRAM_NAME", '"AnCalculator :)"'
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = '17'
        targetCompatibility = '17'
    }
    buildFeatures {
        buildConfig = true
    }
    viewBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'com.google.android.gms:play-services-ads-lite:22.6.0'
    testImplementation 'junit:junit:4.13.2'

    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
}

def commitCount() {
    final def stdout = new ByteArrayOutputStream()
    description = "git rev-list --full-history --all"
    println(description)
    exec {
        commandLine 'git', 'rev-list', '--full-history', '--all'
        standardOutput = stdout
    }
    final String output = stdout.toString()
    final List<String> commits = output.split("\n")
    final int code = commits.size()
    println("Version Code: " + code)
    return code
}